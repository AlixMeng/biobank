#!/usr/bin/perl

use warnings;
use strict;
use Getopt::Long;
use File::Basename;
use Pod::Usage;
use Term::ReadKey;
use Data::Dumper;

=head1 NAME

chkconfig - Checks for a valid deployment configuration environment for a
Biobank server.

=head1 USAGE

chkconfig [OPTIONS]

=head1 OPTIONS

  --database DBNAME  The name of the dataabse used by the Biobank applicatiion. Defaults
                     to "biobank".
  --host DBHOST      The hostname of the machine running the MySQL server. Defaults to
                     localhost if not specified.
  --user DBUSER      The user to use on the MySQL server.
  --passwrd PWD      The password to use on the MySQL server. If not specified the
                     user will be prompted to enter it.
  --help             Help text.
  --man              Full documentation.

=cut

no strict 'refs';

my $MYSQL = "/usr/bin/mysql";
my $GREP = "/bin/grep";
my $SED = "/bin/sed";
my $DBHOST = "localhost";
my $DBNAME = "biobank";
my $DBUSER = "";
my $DBPWD = "";
my $RESULT = 1;
my $help = 0;
my $man = 0;

GetOptions ('database=s' => \$DBNAME,
            'host=s' => \$DBHOST,
            'user=s' => \$DBUSER,
            'password=s' => \$DBPWD,
            'man' => \$man,
            'help|?'   => \$help) or pod2usage(2);

pod2usage(1) if $help;
pod2usage(-exitstatus => 0, -verbose => 2) if $man;

if ($DBUSER eq "") {
    die "ERROR: database user name not specified\n";
}

if ($DBPWD eq "") {
    print "Enter the MySQL password for user $DBUSER: ";
    ReadMode 'noecho';
    $DBPWD = ReadLine 0;
    chomp $DBPWD;
    ReadMode 'normal';
    print "\n";
}

if ($DBPWD eq "") {
    die "ERROR: database user password not specified\n";
}

sub mysqlCmd {
    my $dbname = shift;
    my $cmd = shift;

    if (! defined($cmd)) { die "command is empty"; }
    if (! defined($dbname)) { die "database name not specified"; }
    return `$MYSQL -h$DBHOST -u$DBUSER -p$DBPWD -B --skip-column-names -e \"$cmd\" $DBNAME`;
}

sub checkMysqlInnoDB {
    my $chk_innodb = (`$MYSQL -h$DBHOST -u$DBUSER -p$DBPWD -e "show variables" mysql |
        $GREP -e "^storage_engine"`  =~ /InnoDB/);

    return new TestResult->new("checking MySQL for InnoDB...", $chk_innodb, 1);
}

sub checkMysqlLowerCaseTableNames {
    my $lcnames = (`$MYSQL -h$DBHOST -u$DBUSER -p$DBPWD -e "show variables" mysql |
        $GREP -e "^lower_case_table_names"` =~ /1$/);
    return new TestResult->new("checking MySQL for lower case table names...", $lcnames, 1);
}

sub checkJbossHomeEnv {
    my $env = defined($ENV{'JBOSS_HOME'}) && (-d "$ENV{'JBOSS_HOME'}");
    return new TestResult->new("checking if JBOSS_HOME environment variable defined and valid...", $env, 1);
}

sub checkEclipseHomeEnv {
    my $env = defined($ENV{'ECLIPSE_HOME'}) && (-d "$ENV{'ECLIPSE_HOME'}");
    return new TestResult->new("checking if ECLIPSE_HOME environment variable defined and valid...", $env, 1);
}

sub checkEclipseDeltaPack {
    my $present = 0;
    if (defined($ENV{'ECLIPSE_HOME'})) {
        my $path = $ENV{'ECLIPSE_HOME'}
            . "/plugins/org.eclipse.swt.win32.win32.x86.source_3.7.2.v3740f.jar";
        $present = 1 if (-f "$path");
    }
    return new TestResult->new("checking for Eclipse delta pack...",  $present, 1);
}

sub checkKeystoreFile {
    my $present = 1 if (-f "security-logs/jboss/biobank.keystore");
    return new TestResult->new("checking for SSL certificate file...", $present, 1);
}

sub checkDbExists() {
    my $cmd = "SELECT SCHEMA_NAME FROM INFORMATION_SCHEMA.SCHEMATA "
        . "WHERE SCHEMA_NAME = '$DBNAME'";
    my $present = (mysqlCmd('biobank', $cmd) =~ /^$DBNAME$/);
    return new TestResult->new("checking for databse $DBNAME...", $present, 0);
}

sub checkDbTablesInnoDb() {
    my $cmd = "SELECT TABLE_NAME FROM information_schema.TABLES "
        . "WHERE TABLE_SCHEMA = '$DBNAME' AND engine = 'InnoDB'";

    my @innoTables = mysqlCmd($DBNAME, $cmd);
    my @tables = mysqlCmd($DBNAME, 'SHOW TABLES');
    my @nonInnoDbTables = ();

    #report the bad tables
    foreach (grep { not $_ ~~ @innoTables } @tables) {
        chomp($_);
        push (@nonInnoDbTables, $_);
    }

    my $result = new TestResult->new("checking if database tables are InnoDB...",
                                     ($#nonInnoDbTables < 0), 0);

    foreach (@nonInnoDbTables)  {
        push(@ {$result->{extraMsgs} }, "\ttable \"$_\" is not InnoDB");
    }
    return $result;
}

my @checkFuncs = ('checkMysqlInnoDB',
                  'checkMysqlLowerCaseTableNames',
                  'checkDbExists',
                  'checkDbTablesInnoDb',
                  'checkJbossHomeEnv',
                  'checkKeystoreFile',
                  'checkEclipseDeltaPack');

foreach (@checkFuncs) {
    my $test = &$_;

    (! $test->{required}) && print "OPTIONAL ";
    print $test->{msg}, "\n", ($test->{result} ? "[PASS]" : "[FAIL]"), "\n";

    foreach ( @{ $test->{extraMsgs} } ) {
        print $_, "\n";
    }

    ($test->{required}) && ($RESULT &= $test->{result});
}

(! $RESULT) && print "tests FAILED\n";

exit !$RESULT;


package TestResult;

sub new {
    my $proto = shift;
    my $class = ref $proto || $proto;
    my $self = {
                msg => shift,
                result => shift,
                required => shift,
                extraMsgs => [ ],
                };
    $self = bless $self, $class;

    return $self;
}


# Local Variables:
# compile-command: "cd $HOME/proj/cbsr/biobank && scripts/chkconfig"
# End:

